(define (brainfuck ncells input program)
  (define (prev-opening-bracket i)
    (let loop ((i i) (nest 0))
      (case (string-ref program i)
        ((#\[) (if (<= nest 1) i (loop (- i 1) (- nest 1))))
        ((#\]) (loop (- i 1) (+ nest 1)))
        (else  (loop (- i 1) nest)))))
  (define (next-closing-bracket i)
    (let loop ((i i) (nest 0))
      (case (string-ref program i)
        ((#\[) (loop (+ i 1) (+ nest 1)))
        ((#\]) (if (<= nest 1) i (loop (+ i 1) (- nest 1))))
        (else  (loop (+ i 1) nest)))))
  (let ((cells (make-vector ncells 0)))
    (let loop ((c 0) (i 0))
      (unless (= i (string-length program))
        (let ((insn (string-ref program i))
              (cell (vector-ref cells c)))
          (case insn
            ((#\>)
             (loop (+ c 1) (+ i 1)))
            ((#\<)
             (loop (- c 1) (+ i 1)))
            ((#\+)
             (vector-set! cells c (+ cell 1))
             (loop c (+ i 1)))
            ((#\-)
             (vector-set! cells c (- cell 1))
             (loop c (+ i 1)))
            ((#\.)
             (display (integer->char cell))
             (loop c (+ i 1)))
            ((#\,)
             (vector-set! cells c (input))
             (loop c (+ i 1)))
            ((#\[)
             (loop c (+ 1 (if (= 0 cell) (next-closing-bracket i) i))))
            ((#\])
             (loop c (if (= 0 cell) (+ i 1) (prev-opening-bracket i))))
            (else
             (error "Huh?"))))))))

(brainfuck
 9000 eof-object
 (string-append
  "++++++++[>++++[>++>+++>+++>+<<<<-]>+>->+>>+[<]<-]>>.>>---.++++++"
  "+..+++.>.<<-.>.+++.------.--------.>+.>++."))
